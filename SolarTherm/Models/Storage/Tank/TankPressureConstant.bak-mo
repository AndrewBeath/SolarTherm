within SolarTherm.Models.Storage.Tank;
model TankPressureConstant
  extends Interfaces.Models.StorageFluid;
  SI.Mass m;
  parameter SI.Temperature T_start=from_degC(500);
  SI.AbsolutePressure p_top=1e5;
  //parameter SI.AbsolutePressure p_top=1e5;
  Medium.BaseProperties medium;
  SI.AbsolutePressure p "Pressure";
  SI.Diameter D=18.667 "Diameter";
  SI.Height H=20 "Height";
  Real L "Level in %";
  parameter Real L_start=70 "Inital level in %";

  Medium.ThermodynamicState state_i=Medium.setState_pTX(1e5,T_start);
  SI.Volume V_t=(H*pi*D^2)/4;
  SI.Volume V;
  SI.AbsolutePressure p_bot;
initial equation
  medium.h=Medium.specificEnthalpy(state_i);
  m=Medium.density(state_i)*V_t*L_start/100;
equation
  p=(p_top+p_bot)/2;
  fluid_a.p=p_top;
  p_bot=medium.d*g_n*H*(L/100)+p_top;
  fluid_b.p=p_bot;
  medium.p=p;
  fluid_a.h_outflow=medium.h;
  fluid_b.h_outflow=medium.h;
  der(m)=fluid_a.m_flow+fluid_b.m_flow;
  m*der(medium.h)+der(m)*medium.h=fluid_a.m_flow*inStream(fluid_a.h_outflow)+fluid_b.m_flow*medium.h;

  V=m/medium.d;
  L=100*V/V_t;
  annotation (Icon(coordinateSystem(preserveAspectRatio=false)), Diagram(
        coordinateSystem(preserveAspectRatio=false)));
end TankPressureConstant;
